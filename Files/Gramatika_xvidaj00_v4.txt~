	/*		Program		*/
<PROGRAM>				-> <FUN_SEQ>

	/*		Deklaracia/definicia funkcie alebo main		*/
<FUN_SEQ>				-> int <DEC_OR_DEF_OR_MAIN> <FUN_SEQ>
<FUN_SEQ>				-> <FUN_TYPE> <FUN_DEC_OR_DEF> <FUN_SEQ>
<FUN_SEQ>				-> E
<FUN_TYPE>				-> double
<FUN_TYPE>				-> string
<DEC_OR_DEF_OR_MAIN>	-> main ( ) <COM_SEQ>
<DEC_OR_DEF_OR_MAIN>	-> id ( <PARAMS> ) <DEC_OR_DEF>
<FUN_DEC_OR_DEF>		-> id ( <PARAMS> ) <DEC_OR_DEF>
<DEC_OR_DEF>			-> <COM_SEQ>
<DEC_OR_DEF>			-> ;
<PARAMS>				-> <TYPE> id <PARAMS_N>
<PARAMS>				-> E
<PARAMS_N>				-> , <TYPE> id <PARAMS_N>
<PARAMS_N>				-> E


	/*		Premenna 	*/
<VAR_DEF>		-> <TYPE> id <VAR_INIT> ;
<VAR_DEF>		-> auto id = <EXPR> ;
<TYPE>			-> int
<TYPE>			-> double
<TYPE> 			-> string
<VAR_INIT>		-> = <EXPR>
<VAR_INIT>		-> E


	/* 		Prikaz priradenia	*/
<ASSIGN>		-> id = <EXPR> ;


	/*		Podmieneny prikaz 	*/
<COND>			-> if ( <EXPR> ) <COM_SEQ> else <COM_SEQ>


	/*  	For cyklus			*/
<FOR_LOOP>		-> for ( <VAR_DEF> <EXPR> ; <FOR_ASSIGN> ) <COM_SEQ>
<FOR_ASSIGN>		-> id = <EXPR>


	/*		Volanie funkcie		*/
<FUN_CALL>		-> id = id( <TERM_OR_EPS> ) ;
<TERM_OR_EPS>	-> <TERM> <TERM_OR_EPS_N>
<TERM_OR_EPS>	-> E
<TERM_OR_EPS_N>	-> , <TERM> <TERM_OR_EPS_N>
<TERM_OR_EPS_N>	-> E
<TERM>			-> int
<TERM>			-> double
<TERM>			-> string
<TERM>			-> id

	/*		Prikaz navratu z funkcie	*/
<FUN_RET>		-> return <EXPR> ;


	/*		Prikaz pre nacitanie hodnoty zo vstupu		*/
<CIN>			-> cin >> id <CIN_N> ;
<CIN_N>			-> >> id <CIN_N>
<CIN_N>			-> E


	/*		Prikaz pro vypis hodnot 	*/
<COUT>			-> cout << <COUT_TERM> ;
<COUT_TERM>		-> <TERM> <COUT_TERM_N>
<COUT_TERM_N>	-> << <COUT_TERM>
<COUT_TERM_N>	-> E 


	/* 		Slozeny prikaz 		*/
<COM_SEQ>			-> { <DEF_OR_COM> }
<DEF_OR_COM>		-> <VAR_DEF> <DEF_OR_COM_N>
<DEF_OR_COM>		-> <COMS> <DEF_OR_COM_N>
<COMS>				-> id = <ASSIGN_OR_FUNCALL>
<ASSIGN_OR_FUNCALL> -> <EXPR>
<ASSIGN_OR_FUNCALL>	-> id( <TERM_OR_EPS> )
<COMS>				-> <COND>
<COMS>				-> <FOR_LOOP>
<COMS>				-> <FUN_RET>
<COMS>				-> <CIN>
<COMS>				-> <COUT>
<DEF_OR_COM>		-> E
<DEF_OR_COM_N>		-> <VAR_DEF> <DEF_OR_COM_N>
<DEF_OR_COM_N>		-> <COMS> <DEF_OR_COM_N>
<DEF_OR_COM_N>		-> E


<EXPR>				-> expr
<EXPR>				-> E



